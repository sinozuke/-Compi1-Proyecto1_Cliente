package BackEnd.Analizadores;

import java_cup.runtime.*;
import javax.swing.JOptionPane;
import BackEnd.DOA.Objetos.Sistema.Error;
import BackEnd.DOA.Objetos.Relevantes.Producto;
import BackEnd.DOA.Objetos.Relevantes.Tienda;
import BackEnd.DOA.Objetos.Sistema.Acess;
import java.util.ArrayList;

/*
import static compi1.proyecto1_cliente.pkg201403775.Compi1Proyecto1_Cliente201403775.Log_in;
import static compi1.proyecto1_cliente.pkg201403775.Compi1Proyecto1_Cliente201403775.Catalogo_Tiendas;
import static compi1.proyecto1_cliente.pkg201403775.Compi1Proyecto1_Cliente201403775.usuario;
import static compi1.proyecto1_cliente.pkg201403775.Compi1Proyecto1_Cliente201403775.Catalogo_Productos;
import static compi1.proyecto1_cliente.pkg201403775.Compi1Proyecto1_Cliente201403775.errores;
*/

parser code
{:
    /**Metodo al que se llama automáticamente ante algún error sintactico.*/
    public void syntax_error(Symbol s){
		System.out.println("Error en la Línea " + (s.right+1) +" Columna "+s.left+ ". Identificador "
        +s.value + " no reconocido.");
    }

    /**Metodo al que se llama en el momento en que ya no es posible una recuperación de
    errores.*/
    public void unrecovered_syntax_error(Symbol s) throws java.lang.Exception{
		System.out.println("Error en la Línea " + (s.right+1)+ "Columna "+s.left+". Identificador " +
        s.value + " no reconocido.");
    }
:}

action code
{:

		
:}
terminal String numero,cadena,bool;
terminal DI,DF,Igual,imagen;
terminal Reply,Access,Registro,Modificar,Eliminacion,Tienda,usuario,Producto,ERR,Lista;
terminal TIPO,PO,FILA,COLUMNA,DESC;
terminal Id,Direccion,Nombre,Telefono,Codigo,Propietario,Cantidad,Marca,Color,Tamano,Sucursal;

non terminal INICIA;
non terminal CONTENIDO,CONTENIDOP,R1;
non terminal Tienda TCP,TM,TE,TINFO,TINFOP,TCS;
non terminal Producto PR,PM,PE,PCP,PINFO,PINFOP,PCS;
non terminal Error ERRC,ERRCP,POC,POCP;
non terminal Acess LOG,DATALOGP;
non terminal ArrayList<Producto> PC;
non terminal ArrayList<Tienda>  TC;
non terminal TR;

start with INICIA;

INICIA ::= DI Reply DI CONTENIDO DI Reply DF                                            {::};

CONTENIDO ::= CONTENIDO CONTENIDOP                                                      {::}
            |CONTENIDOP                                                                 {::};

CONTENIDOP ::=   DI usuario DI LOG  DI usuario DF                                       {::}
                |DI Tienda DI TCS DI Tienda DF                                          {::}
                |DI usuario DI R1  DI usuario DF                                       {::}
                |DI Producto DI PCS DI Producto DF                                      {::}
                |DI ERR DI ERRC:val DI ERR DF
{:
                    errores.add(val);
:}
                |DI Lista DI PC:val  DI Lista DF
{:
                val.stream().forEach((Producto p)->{
                    Catalogo_Productos.agregar_Producto(p);
                });
                
                Catalogo_Productos.btnmodi.setEnabled(true);
                Catalogo_Productos.catalogo_productos.setEnabled(true);
                Catalogo_Productos.btneli.setEnabled(true);
                Catalogo_Productos.carga_productos();
                Catalogo_Productos.setVisible(true);
:}
                |DI Lista DI TC:val  DI Lista DF
{:
                val.stream().forEach((Tienda t)->{
                    Catalogo_Tiendas.ingresar_tienda(t);
                });
                
                Catalogo_Tiendas.btneliminar.setEnabled(true);
                Catalogo_Tiendas.btnmodi.setEnabled(true);
                Catalogo_Tiendas.btnmostrar.setEnabled(true);
                Catalogo_Tiendas.catalogo_tiendas.setEnabled(true);
                Catalogo_Tiendas.setVisible(true);
                Catalogo_Tiendas.cargar_tiendas();
                Catalogo_Tiendas.setVisible(true);
:}
                |DI Lista DI DI Lista DF
{:		
                if(Catalogo_Productos.esta){
                    JOptionPane.showMessageDialog(null, "No Posees Productos! :(");
                    Catalogo_Productos.btnmodi.setEnabled(false);
                    Catalogo_Productos.catalogo_productos.setEnabled(false);
                    Catalogo_Productos.btneli.setEnabled(false);
                    Catalogo_Productos.setVisible(true);
                }else if(Catalogo_Tiendas.este){
                    JOptionPane.showMessageDialog(null, "No Posees Tiendas! :(");
                    Catalogo_Tiendas.btneliminar.setEnabled(false);
                    Catalogo_Tiendas.btnmodi.setEnabled(false);
                    Catalogo_Tiendas.btnmostrar.setEnabled(false);
                    Catalogo_Tiendas.catalogo_tiendas.setEnabled(false);
                    Catalogo_Tiendas.setVisible(true);
                }
:};

ERRC ::=     ERRC:val1 ERRCP:val2
{:
            RESULT = val1;
            if(!val2.getTipo().equals("vacio") && RESULT.getTipo().equals("vacio")){
                RESULT.setTipo(val2.getTipo());
            }else if(!val2.getDescripccion().equals("vacio") && RESULT.getDescripccion().equals("vacio")){
                RESULT.setDescripccion(val2.getDescripccion());
            }else if(val2.getFila()!=0 && RESULT.getFila()==0){
                RESULT.setFila(val2.getFila());
            }else if(val2.getColumna()!=0 && RESULT.getColumna()==0){
                RESULT.setColumna(val2.getColumna());
            }
:}
            |ERRCP:val
{:
            RESULT = val;
:};

ERRCP ::=    DI  TIPO DI cadena:val DI TIPO DF
{:
                RESULT = new Error();
                RESULT.setTipo(val);
:}
            |DI  PO DI POC:val DI PO DF
{:
                RESULT = val;
:}
            |DI DESC DI cadena:val DI DESC DF
{:
                RESULT = new Error();
                RESULT.setDescripccion(val);    
:};

POC ::=  POC:val1 POCP:val2
{:
            RESULT = val1;
            if(val2.getFila()!=0 && RESULT.getFila()==0){
                RESULT.setFila(val2.getFila());
            }else if(val2.getColumna()!=0 && RESULT.getColumna()==0){
                RESULT.setColumna(val2.getColumna());
            }
:}
        |POCP:val
{:
            RESULT = val;
:};

POCP ::=     DI FILA DI numero:val DI FILA DF
{:
                RESULT = new Error();
                RESULT.setFila(Integer.parseInt(val));
:}
            |DI COLUMNA DI numero:val DI COLUMNA DF
{:
                RESULT = new Error();
                RESULT.setColumna(Integer.parseInt(val));
:};

PC ::=   PC:val1 PCP:val2
{:
                RESULT = val1;
                RESULT.add(val2);
:}
        |PCP:val
{:
                RESULT = new ArrayList();
                RESULT.add(val);
:};

TC ::=   TC:val1 TCP:val2
{:
                RESULT = val1;
                RESULT.add(val2);
:}
        |TCP:val1
{:
                RESULT = new ArrayList();
                RESULT.add(val1);
:};

PCP ::=   DI Producto DI PINFO:val DI Producto DF
{:
                RESULT = val;
:};

TCP ::=   DI Tienda DI TINFO:val DI Tienda DF
{:
                RESULT = val;
:};
                                                                  

PINFO ::=    PINFO:val1 PINFOP:val2
{:
                RESULT = val1;

                if(val2.getId()!=0 && RESULT.getId()==0){
                    RESULT.setId(val2.getId());
                }else if(!val2.getNombrre().equals("vacio") && RESULT.getNombrre().equals("vacio")){
                    RESULT.setNombrre(val2.getNombrre());
                }else if(val2.getCantidad()!=0 && RESULT.getCantidad()==0){
                    RESULT.setCantidad(val2.getCantidad());
                }else if(!val2.getMarca().equals("vacio") && RESULT.getMarca().equals("vacio")){
                    RESULT.setMarca(val2.getMarca());
                }else if(!val2.getColor().equals("vacio") && RESULT.getColor().equals("vacio")){
                    RESULT.setColor(val2.getColor());
                }else if(val2.getTamaño()!=0 && RESULT.getTamaño()==0){
                    RESULT.setTamaño(val2.getTamaño());
                }else if(val2.getSucursal()!=0 && RESULT.getSucursal()==0){
                    RESULT.setSucursal(val2.getSucursal());
                }else if(!val2.getImg().equals("vacio") && RESULT.getImg().equals("vacio")){
                    RESULT.setImg(val2.getImg());
                }
:}
            |PINFOP:val
{:
                RESULT = val;
:};

TINFO ::=    TINFO:val1 TINFOP:val2
{:		
                RESULT = val1;
                if(val2.getCodigo()!=0 && RESULT.getCodigo()==0){
                    RESULT.setCodigo(val2.getCodigo());
                }else if(!val2.getNombre().equals("vacio") && RESULT.getNombre().equals("vacio")){
                    RESULT.setNombre(val2.getNombre());
                }else if(val2.getPropietario()!=0 && RESULT.getPropietario()==0){
                    RESULT.setPropietario(val2.getPropietario());
                }else if(!val2.getDirreccion().equals("vacio") && RESULT.getDirreccion().equals("vacio")){
                    RESULT.setDirreccion(val2.getDirreccion());
                }else if(!val2.getTelefono().equals("vacio") && RESULT.getTelefono().equals("vacio")){
                    RESULT.setTelefono(val2.getTelefono());
                }else if(!val2.getImg().equals("vacio") && RESULT.getImg().equals("vacio")){
                    RESULT.setImg(val2.getImg());
                }
:}
            |TINFOP:val
{:
                RESULT = val;
:};


PINFOP ::=   DI Codigo DI numero:val DI Codigo DF
{:
                RESULT = new Producto();
                RESULT.setId(Integer.parseInt(val));
:}
            |DI Nombre DI cadena:val DI Nombre DF
{:
                RESULT = new Producto();
                RESULT.setNombrre(val);
:}
            |DI Cantidad DI numero:val DI Cantidad DF
{:
                RESULT = new Producto();
                RESULT.setCantidad(Integer.parseInt(val));
:}
            |DI Marca DI cadena:val DI Marca DF
{:
                RESULT = new Producto();
                RESULT.setMarca(val);
:}
            |DI Color DI cadena:val DI Color DF
{:
                RESULT = new Producto();
                RESULT.setColor(val);
:}
            |DI Tamano DI numero:val DI Tamano DF
{:
                RESULT = new Producto();
                RESULT.setTamaño(Integer.parseInt(val));
:}
            |DI Sucursal DI numero:val DI Sucursal DF
{:
                RESULT = new Producto();
                RESULT.setSucursal(Integer.parseInt(val));
:}
            |DI imagen DI cadena:val DI imagen DF
{:
                RESULT = new Producto();
                RESULT.setImg(val.replaceAll("\"",""));
:};


TINFOP ::=   DI Codigo DI numero:val DI Codigo DF
{:
                RESULT = new Tienda();
                RESULT.setCodigo(Integer.parseInt(val));
:}
            |DI Propietario DI numero:val DI Propietario DF
{:
                RESULT = new Tienda();
                RESULT.setPropietario(Integer.parseInt(val));
:}
            |DI Nombre DI cadena:val DI Nombre DF
{:
                RESULT = new Tienda();
                RESULT.setNombre(val);
:}
            |DI Direccion DI cadena:val DI Direccion DF
{:
                RESULT = new Tienda();
                RESULT.setDirreccion(val);
:}
            |DI Telefono DI numero:val DI Telefono DF
{:
                RESULT = new Tienda();
                RESULT.setTelefono(val);
:}
            |DI imagen DI cadena:val DI imagen DF
{:
                RESULT = new Tienda();
                RESULT.setImg(val.replaceAll("\"",""));
:};

PCS ::= PR                                                                             {::}
        |PM                                                                             {::}
        |PE                                                                             {::};

TCS ::=  TR                                                                              {::}
        |TM                                                                              {::}
        |TE                                                                             {::};

TR ::= DI Registro Id Igual numero:val1 DI bool:val2  DI Registro DF
{:
    if(val2.equals("True")){
        JOptionPane.showMessageDialog(null,"Registro Exitoso de Tienda No."+val1);
    }else if(val2.equals("False")){
        JOptionPane.showMessageDialog(null,"Registro Erroneo de tienda");
    }
:};

PR ::=  DI Registro Id Igual numero:val1 Sucursal Igual numero:val2 DI bool:val3  DI Registro DF
{:
    if(val3.equals("True")){
        JOptionPane.showMessageDialog(null,"Registro Exitoso de producto No."+val1 + " de la sucursal NO."+val2);
    }else if(val3.equals("False")){
        JOptionPane.showMessageDialog(null,"Registro Erroneo de producto");
    }
:};

PM ::=  DI Modificar Id Igual numero:val1 Sucursal Igual numero:val2 DI bool:val3 DI Modificar DF
{:
    if(val3.equals("True")){
        JOptionPane.showMessageDialog(null,"Modificacion Exitosa de Producto No."+val1 +" de la sucursal NO."+val2);
    }else if(val3.equals("False")){
        JOptionPane.showMessageDialog(null,"Modificacion Erronea de Producto");
    }

:};

PE ::=  DI Eliminacion Id Igual numero:val1 Sucursal Igual numero:val2 DI bool:val3 DI Eliminacion DF
{:
                if(val3.equals("True")){
                    Catalogo_Productos.get_productos();
                    JOptionPane.showMessageDialog(null,"Eliminacion Exitosa de Producto No."+val1+" de la sucural NO."+val1);
                }else if(val3.equals("False")){
                    JOptionPane.showMessageDialog(null,"eliminacion Fallida de Teinda");
                    Catalogo_Productos.setVisible(true);
                }

:};

TM ::= DI Modificar Id Igual numero:val1 DI bool:val2 DI Modificar DF
{:
    if(val2.equals("True")){
        JOptionPane.showMessageDialog(null,"Modificacion Exitosa de Tienda No."+val1);
    }else if(val2.equals("False")){
        JOptionPane.showMessageDialog(null,"Modificacion Fallida de tienda");
    }
:};
TE ::= DI Eliminacion Id Igual numero:val1 DI bool:val2 DI Eliminacion DF
{:
    if(val2.equals("True")){
        Catalogo_Tiendas.get_tiendas();
        JOptionPane.showMessageDialog(null,"Eliminacion Exitosa de Tienda No."+val1);
    }else if(val2.equals("False")){
        JOptionPane.showMessageDialog(null,"Eliminacion Erronea Erroneo de tienda");
        Catalogo_Tiendas.setVisible(true);
    }
:};

R1 ::= DI Registro Id Igual numero:val1 DI bool:val2  DI Registro DF
{:
                if(val2.equals("True")){
                    JOptionPane.showMessageDialog(null,"Registro Exitoso");
                }else{
                    JOptionPane.showMessageDialog(null,"El Registro del Usuario " + val1 + " ha Fallado");
                }
:}

        |error usuario {::};

LOG ::=  LOG:val1 DATALOGP:val2
{:
                RESULT = new Acess();
                if(val1.getId()!=null){
                    RESULT.setId(val1.getId());
                    RESULT.setValor(val2.getValor());
                    RESULT.concatenar(val1.getreply());
                    RESULT.concatenar(val2.getreply());
                }else{
                    RESULT.setId(val2.getId());
                    RESULT.setValor(val1.getValor());
                    RESULT.concatenar(val2.getreply());
                    RESULT.concatenar(val1.getreply());
                }
                switch (RESULT.getValor()) {
                    case "True":
                        usuario.setId(Integer.parseInt(RESULT.getId()));
                        Catalogo_Tiendas.get_tiendas();
                        Log_in.setVisible(false);
                        JOptionPane.showMessageDialog(null,"Bienvenido usuario " + RESULT.getId() + "\n En Momentos se mostrara la ventana de Tiendas en Respuesta al Servidor... se Paciente");
                        break;
                    case "False":
                        JOptionPane.showMessageDialog(null,"Error: Acesso Denegado, la contraseña es incorrecta");
                        break;
                    case "Fail":
                        JOptionPane.showMessageDialog(null,"Error: Acesso Fallido, no Existe Usuario");
                        break;
                    default:
                        break;
                }
:}
        |DATALOGP:val                                                                   {:  RESULT = val; :};

DATALOGP ::=  DI Id DI numero:val DI Id DF                                              
{:
                RESULT = new Acess();
                RESULT.setId(val);
                RESULT.concatenar("\t $id$ ");
                RESULT.concatenar(val);
                RESULT.concatenar(" $id-$\n");
:}
             |DI Access DI bool:val DI Access DF
{:
                RESULT = new Acess();
                RESULT.setValor(val);
                RESULT.concatenar("\t $access$ ");
                RESULT.concatenar(val);
                RESULT.concatenar(" $access-$\n");
:};